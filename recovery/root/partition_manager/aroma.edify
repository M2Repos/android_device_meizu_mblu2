# set default colourspace
ini_set("force_colorspace", "rgba");

# set dp size
ini_set("dp", "3");

# set theme and font
theme("materialize_indigo");
fontresload("0", "fonts/Roboto-Light.ttf;", "10");  #-- General text
fontresload("1", "fonts/Roboto-Light.ttf;", "14");  #-- Title text

# set device from ro.product.device prop, abort if not possible
setvar("device", sysprop("ro.product.device"));
if (!getvar("device") == "mblu2" ) then
	# something wrong with this device/recovery
	ini_set("text_next", "Exit");
	ini_set("icon_next", "");
	textbox(
		"Error",
		"Wrong device",
		"@warning",
		"This is only for mblu2 (Meizu M2) devices."
	);
	exit("");
endif;

# extract common files
ziptotmp("version.prop", "version.prop");
ziptotmp("changelog.txt", "changelog.txt");
ziptotmp("disclaimer.txt", "disclaimer.txt");
ziptotmp("credits.txt", "credits.txt");

# set common variables
ini_set("rom_name", file_getprop("/tmp/aroma/version.prop", "name"));
ini_set("rom_author", "Dinolek (see Credits for more)");
ini_set("rom_device", getvar("device"));

# show disclaimer
agreebox(
	"Disclaimer",
	"Please read and acknowledge the following information",
	"@warning",
	read("/tmp/aroma/disclaimer.txt"),
	"I understand and have made a Recovery backup",
	"You must acknowledge the disclaimer to use " + ini_get("rom_name") + "!"
);

###################
### Main menu
###################

gotolabel("start");
ini_set("text_next", "Next");
ini_set("icon_next", "@next");

# gather information about existing partition map
# Not necessary, happens in a split second
setvar("partition_status", exec("/sbin/sh", "/partition_manager/get_partition_status.sh"));
setvar("partition_status_text", "Unrecognized");
setvar("repartition_subtext", "Unrecognized partition layout");
if (getvar("partition_status") == 1) then
	setvar("partition_status_text", "Stock");
	setvar("repartition_subtext", "Repartition device for Treble capability");
endif;
if (getvar("partition_status") == 2) then
	setvar("partition_status_text", "Treble");
	setvar("repartition_subtext", "Restore stock partition map");
endif;

menubox(
	"Welcome to " + ini_get("rom_name") + "!",
	"Detected Device: <b>" + getvar("device") + "</b>\n" +
	"Detected partition map: " + getvar("partition_status_text"),
	"@home",
	"choice_main_menu.prop",
	"Repartition", getvar("repartition_subtext"), "@chip",
	"Changelog", "View changelog for this version", "@changelog",
	"Credits", "See thanks and credits", "@credits",
	"Exit", "Exit and return to Recovery", "@exit"
);

###################
### Repartition
###################

if prop("choice_main_menu.prop", "selected") == "1" then
	# Partitions
	if (getvar("partition_status") == 0) then
		form(
			"Repartition",
			"Choose partition table.",
			"@chip",
			"choice_repartition.prop",
			"treble",	"Repartition to treble",	"Rename custom to vendor and resize system",	"select.selected",
			"stock",    "Repartition to stock",		"Rename vendor to custom and resize system",	"select"
		);
	endif;
	if (getvar("partition_status") == 1 || prop("choice_repartition.prop", "root") == "treble") then
		writetmpfile("choice_repartition.prop", "root=treble");
		setvar("partition_subtitle", "You are about to repartition for Treble support.");
	else
		writetmpfile("choice_repartition.prop", "root=stock");
		setvar("partition_subtitle", "You are about to repartition to stock partitions.");
	endif;

	ini_set("text_next", "Repartition");
	ini_set("icon_next", "@undo");

	agreebox(
		"Repartition for Treble",
		getvar("partition_subtitle"),
		"@warning",
		"\n\n<#c00><b>This will COMPLETELY WIPE your USERDATA, including INTERNAL STORAGE, so make sure you have backed up anything you want to keep!</b></#>\n\n\n" +
		"Please check the box below to confirm.",
		"I wish to repartition this device",
		"Check the box to confirm!"
	);
endif;

###################
### Changelog and Credits
###################

if prop("choice_main_menu.prop","selected")=="2" then
	ini_set("text_next", "Return  ");
	ini_set("icon_next", "@home");
	textbox(
		"Changelog",
		"Changelog up to this version",
		"@changelog",
		read("/tmp/aroma/changelog.txt")
	);
	goto("start");
endif;

if prop("choice_main_menu.prop","selected")=="3" then
	ini_set("text_next", "Return  ");
	ini_set("icon_next", "@home");
	textbox(
		"Credits",
		"Thanks and credits for Partition Manager",
		"@credits",
		read("/tmp/aroma/credits.txt")
	);
	goto("start");
endif;

###################
### Exit
###################

if prop("choice_main_menu.prop","selected")=="4" then
	exit("");
endif;

###################
### Installation
###################

if prop("choice_main_menu.prop", "selected") == "1" then
	# cache the partition status value for install process
	write("/tmp/partition_status", getvar("partition_status"));

	ini_set("text_next", "Reboot TWRP");
	ini_set("icon_next", "@exit");
	setvar("retstatus",
		install(
			"Repartitioning",
			"<b>"+ini_get("rom_name")+"</b> is performing the repartition.\n\n"+
			"This may take a minute.",
			"@chip",
			""
		)
	);
	write("/tmp/do_reboot_recovery", "");
endif;

exit("");
